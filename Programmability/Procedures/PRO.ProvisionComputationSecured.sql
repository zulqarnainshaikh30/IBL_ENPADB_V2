SET QUOTED_IDENTIFIER, ANSI_NULLS ON
GO

/*=====================================
AUTHER : TRILOKI KHANNA
CREATE DATE : 27-11-2019
MODIFY DATE : 27-11-2019
DESCRIPTION : ProvisionComputationSecured 
EXEC pro.ProvisionComputationSecured  @TIMEKEY=25410 
====================================*/
CREATE PROCEDURE [PRO].[ProvisionComputationSecured] 
@TimeKey INT	
WITH RECOMPILE												
AS
  BEGIN
        SET NOCOUNT ON
        BEGIN TRY

		DECLARE @STDkey INT =	(Select ProvisionAlt_Key from DimProvision_SegStd where EffectiveToTimeKey=49999 and ProvisionName='Other Portfolio')

		UPDATE PRO.ACCOUNTCAL  SET SECUREDAMT=0 ,PROVSECURED=0,BANKPROVSECURED=0,RBIPROVSECURED=0

		DROP TABLE IF EXISTS #CTE_ProvAmt	
	 
		SELECT AccountEntityID,CASE WHEN SECAPP='S' AND FinalAssetClassAlt_Key<=2 THEN NetBalance 
									WHEN SECAPP='U' AND FinalAssetClassAlt_Key<=2 THEN 	0
									ELSE UsedRV end AmtForProvCal
			INTO #CTE_ProvAmt
		FROM PRO.ACCOUNTCAL  
	 
	UPDATE A 
	SET SECUREDAMT=CASE WHEN ISNULL(B.ASSETCLASSSHORTNAMEENUM,'STD')='LOS' 
	                                          THEN 0
				                    ELSE ISNULL(P.AmtForProvCal,0)
				                    END
	
		 , PROVSECURED =(CASE WHEN ISNULL(B.ASSETCLASSSHORTNAMEENUM,'STD')='LOS' 
				                    THEN 0
						    ELSE (ISNULL(P.AmtForProvCal,0) * CASE WHEN C.PROVISIONNAME='Corporate Common' THEN ISNULL(A.ProvPerSecured,0) ELSE ISNULL(PR.SECURED_PERCENTAGE,ISNULL(C.PROVISIONSECURED,0))/100 END)  
							 END)

	 	, BANKPROVSECURED =(CASE WHEN ISNULL(B.ASSETCLASSSHORTNAMEENUM,'STD')='LOS' 
				                    THEN 0
						    ELSE (ISNULL(P.AmtForProvCal,0) * CASE WHEN C.PROVISIONNAME='Corporate Common' THEN ISNULL(A.ProvPerSecured,0) ELSE ISNULL(PR.SECURED_PERCENTAGE,ISNULL(C.PROVISIONSECURED,0))/100 END) 
				            END)
		, RBIPROVSECURED =(CASE WHEN ISNULL(B.ASSETCLASSSHORTNAMEENUM,'STD')='LOS' 
				                    THEN 0
						    ELSE (ISNULL(P.AmtForProvCal,0) * ISNULL(PR.SECURED_PERCENTAGE,ISNULL(C.RBIPROVISIONSECURED,0)) /100)
				            END)

		FROM PRO.ACCOUNTCAL A INNER JOIN PRO.CUSTOMERCAL D ON A.CUSTOMERENTITYID=D.CUSTOMERENTITYID
		INNER JOIN DIMASSETCLASS B ON B.EFFECTIVEFROMTIMEKEY<=@TIMEKEY
									AND B.EFFECTIVETOTIMEKEY>=@TIMEKEY      
									AND ISNULL(A.FINALASSETCLASSALT_KEY,1) =B.ASSETCLASSALT_KEY 
		INNER JOIN DIMPROVISION_SEG C ON C.EFFECTIVEFROMTIMEKEY<=@TIMEKEY
								  AND C.EFFECTIVETOTIMEKEY>=@TIMEKEY      
								  AND ISNULL(A.PROVISIONALT_KEY,1) = C.PROVISIONALT_KEY 
		INNER JOIN #CTE_ProvAmt P ON P.AccountEntityID=A.AccountEntityID
		LEFT JOIN CURDAT.PROVISION_REDUCTION PR ON A.CustomerAcID=PR.CustomerACID
													AND PR.EFFECTIVEFROMTIMEKEY<=@TIMEKEY
													AND PR.EFFECTIVETOTIMEKEY>=@TIMEKEY      
		WHERE ISNULL(D.FLGPROCESSING,'N') ='N' AND A.BALANCE > 0  and FINALASSETCLASSALT_KEY>1

	UPDATE A 
	SET SECUREDAMT=ISNULL(P.AmtForProvCal,0)
	  
		 , PROVSECURED = (ISNULL(P.AmtForProvCal,0) * CASE WHEN C.PROVISIONNAME='Corporate Common' THEN ISNULL(A.ProvPerSecured,0) ELSE ISNULL(C.PROVISIONSECURED,0)/100 END)  
						
		, BANKPROVSECURED = (ISNULL(P.AmtForProvCal,0) * CASE WHEN C.PROVISIONNAME='Corporate Common' THEN ISNULL(A.ProvPerSecured,0) ELSE ISNULL(C.PROVISIONSECURED,0)/100 END )
				           
		, RBIPROVSECURED = (ISNULL(P.AmtForProvCal,0) * C.RBIPROVISIONSECURED /100)
				          
	FROM PRO.ACCOUNTCAL A  INNER JOIN PRO.CUSTOMERCAL D ON A.CUSTOMERENTITYID=D.CUSTOMERENTITYID
		INNER JOIN DIMASSETCLASS B ON B.EFFECTIVEFROMTIMEKEY<=@TIMEKEY
									AND B.EFFECTIVETOTIMEKEY>=@TIMEKEY      
									AND ISNULL(A.FINALASSETCLASSALT_KEY,1) =B.ASSETCLASSALT_KEY 
		INNER JOIN DimProvision_SegStd C ON C.EFFECTIVEFROMTIMEKEY<=@TIMEKEY
								  AND C.EFFECTIVETOTIMEKEY>=@TIMEKEY      
								  AND ISNULL(A.PROVISIONALT_KEY,@STDkey) = C.PROVISIONALT_KEY 
		INNER JOIN #CTE_ProvAmt P ON P.AccountEntityID=A.AccountEntityID
	WHERE ISNULL(D.FLGPROCESSING,'N') ='N' AND A.BALANCE > 0 and FinalAssetClassAlt_Key=1
	

	
			UPDATE PRO.ACCOUNTCAL  SET SECUREDAMT=0  WHERE ISNULL(SECUREDAMT,0)<=0
			UPDATE PRO.ACCOUNTCAL  SET PROVSECURED=0  WHERE ISNULL(PROVSECURED,0)<=0
			UPDATE PRO.ACCOUNTCAL  SET BANKPROVSECURED=0  WHERE ISNULL(BANKPROVSECURED,0)<=0
			UPDATE PRO.ACCOUNTCAL  SET RBIPROVSECURED=0  WHERE ISNULL(RBIPROVSECURED,0)<=0
	


	      
UPDATE PRO.ACLRUNNINGPROCESSSTATUS 
	SET COMPLETED='Y',ERRORDATE=NULL,ERRORDESCRIPTION=NULL,COUNT=ISNULL(COUNT,0)+1
	WHERE RUNNINGPROCESSNAME='ProvisionComputationSecured'

	-----------------Added for DashBoard 04-03-2021
--Update BANDAUDITSTATUS set CompletedCount=CompletedCount+1 where BandName='ASSET CLASSIFICATION'
 
END TRY
BEGIN  CATCH
	
	UPDATE PRO.ACLRUNNINGPROCESSSTATUS 
	SET COMPLETED='N',ERRORDATE=GETDATE(),ERRORDESCRIPTION=ERROR_MESSAGE(),COUNT=ISNULL(COUNT,0)+1
	WHERE RUNNINGPROCESSNAME='ProvisionComputationSecured'
END CATCH

        SET NOCOUNT OFF	     
END 










GO